cmake_minimum_required(VERSION 3.12)

# 设置工程属性，如版本，开发语言等
project(compiler VERSION 1.0.1 LANGUAGES CXX)

# 设置CXX与C编译器，可能需要修改
set(CMAKE_CXX_COMPILER "g++")
set(CMAKE_C_COMPILER "gcc")

# 是否输出Bison语法分析时是否输出自动机， 默认输出
# 如果修改为OFF，请删除cmake_build_debug后重新build
set(BISON_OUTPUT_GRAPH OFF CACHE BOOL "Enable/Disable output of a graph of the automaton for Bison")

# 是否使用GravphViz库
set(USE_GRAPHVIZ ON CACHE BOOL "Enable/Disable GraphViz")

# 默认设置为ON，这里可注释掉。开启时会产生compile_commands.json的文件，有了这个文件才能识别出clang-tidy的配置
# Generates a `compile_commands.json` that can be used for autocompletion
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# 这个目前没有用处，可删除
set(CMAKE_INCLUDE_CURRENT_DIR ON)

# 禁止MSVC编译时出现C4819警告
if(MSVC)
	add_compile_options("$<$<C_COMPILER_ID:MSVC>:/utf-8>")
	add_compile_options("$<$<CXX_COMPILER_ID:MSVC>:/utf-8>")
endif()

# graphviz查找文件
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/CMake)

# 查找线程库，避免terminate called after throwing an instance of 'std::system_error'
# find_package(Threads REQUIRED)

# 查找Java，若没有安装则错误。因ANTLR4需要，如果不使用ANTLR4时可删除
# find_package(Java COMPONENTS Runtime REQUIRED)

# 因AST输出图片需要借助Graphviz提供的C++接口来实现，因此若Graphviz没有安装则出错
# 查找graphviz库是否在系统中安装，若安装则设置一系列的变量，如LIBGVC_INCLUDE_DIRS等
find_package(Graphviz REQUIRED)

# 查找flex工具，找不到则出错，注意设置PATHS的路径；FLEX_EXECUTABLE记录flex程序的位置
# 请优先使用find_package，不行用find_program，指定flex的位置
# 注释下一行可查看都在哪里查找路径了
# set(CMAKE_FIND_DEBUG_MODE TRUE)
find_package(FLEX REQUIRED)

# 查找bison工具，找不到则出错，注意设置PATHS的路径；BISON_EXECUTABLE记录bison程序的位置
# 请优先使用find_package，不行用find_program，指定bison的位置
find_package(BISON REQUIRED)

# 如果开启Bison输出具体语法树，则设置该选项
if(BISON_OUTPUT_GRAPH)
	set(BISON_EXTRA_OPTION "--graph=${FLEX_GEN_DIR}/MiniCBison.dot")
else()
	set(BISON_EXTRA_OPTION)
endif()

# flex工具的输入和输出文件
set(FLEX_GEN_DIR ${CMAKE_CURRENT_SOURCE_DIR}/frontend/flexbison/autogenerated)
set(FLEX_INPUT ${CMAKE_CURRENT_SOURCE_DIR}/frontend/flexbison/MiniC.l)
set(FLEX_OUTPUT
	${FLEX_GEN_DIR}/MiniCFlex.cpp
)

# bison工具的输入和输出文件
set(BISON_GEN_DIR ${CMAKE_CURRENT_SOURCE_DIR}/frontend/flexbison/autogenerated)
set(BISON_INPUT
	${CMAKE_CURRENT_SOURCE_DIR}/frontend/flexbison/MiniC.y
)
set(BISON_OUTPUT
	${BISON_GEN_DIR}/MiniCBison.cpp
)

# 前端源代码集合
set(FRONTEND_SRCS

	# 前端共性代码
	frontend/AST.cpp
	frontend/Graph.cpp

	# Flex与Bison相关代码
	${FLEX_OUTPUT}
	${BISON_OUTPUT}
	frontend/flexbison/FlexBisonExecutor.cpp
)

# 后端源代码集合
set(BACKEND_SRCS

	# 后端共性代码
	backend/CodeGenerator.cpp
	backend/CodeGeneratorAsm.cpp

	# 后端产生ARM64汇编指令
	backend/arm64/ILocArm64.cpp
	backend/arm64/InstSelectorArm64.cpp
	backend/arm64/PlatformArm64.cpp
	backend/arm64/CodeGeneratorArm64.cpp

	backend/arm32/SimpleRegisterAllocator.cpp
	backend/arm32/ILocArm32.cpp
	backend/arm32/InstSelectorArm32.cpp
	backend/arm32/PlatformArm32.cpp
	backend/arm32/CodeGeneratorArm32.cpp
)

# 中间IR(ir)源代码集合
set(IR_SRCS
	ir/Generator/IRGenerator.cpp
	ir/Instructions/ArgInstruction.cpp
	ir/Instructions/BinaryInstruction.cpp
	ir/Instructions/CastInstruction.cpp
	ir/Instructions/EntryInstruction.cpp
	ir/Instructions/ExitInstruction.cpp
	ir/Instructions/FuncCallInstruction.cpp
	ir/Instructions/GotoInstruction.cpp
	ir/Instructions/LabelInstruction.cpp
	ir/Instructions/MoveInstruction.cpp
	ir/Instructions/StoreInstruction.cpp
	ir/Instructions/LoadInstruction.cpp
	ir/Types/VoidType.cpp
	ir/Types/LabelType.cpp
	ir/Types/IntegerType.cpp
	ir/Types/FloatType.cpp
	ir/Types/ArrayType.cpp
	ir/CFG.cpp
	ir/IRCode.cpp
	ir/Function.cpp
	ir/Instruction.cpp
	ir/Use.cpp
	ir/User.cpp
	ir/Value.cpp
	ir/Values/ConstInt.cpp
)

# 符号表等共通化代码集合
set(SYMBOLTABLES_SRCS
	symboltable/Module.cpp
	symboltable/ScopeStack.cpp
)

# 系统差异性代码集合
set(UTILS_SRCS
	utils/Common.cpp
	utils/getopt-port.cpp
	utils/Set.cpp
)

# 优化源代码集合
# TODO 增加优化时可在这里指定源代码的相对路径
set(OPT_SRCS)

# 配置创建一个可执行程序，以及该程序所依赖的所有源文件、头文件等
add_executable(${PROJECT_NAME}

	# 主程序
	main.cpp

	# 前端源代码
	${FRONTEND_SRCS}

	# 后端源代码
	${BACKEND_SRCS}

	# 符号表代码
	${SYMBOLTABLES_SRCS}

	# 中间IR代码
	${IR_SRCS}

	# 优化代码，暂时未用
	${OPT_SRCS}

	# 操作系统差异化代码，VC编译时使用
	${UTILS_SRCS}
)

# 设置语言标准C++17，可根据需要调整
set_target_properties(${PROJECT_NAME} PROPERTIES
	CXX_STANDARD 17
	CXX_EXTENSIONS OFF
	CXX_STANDARD_REQUIRED ON
)

# 只保留通用的编译选项
target_compile_options(${PROJECT_NAME} PRIVATE -Wall -Wno-write-strings -Wno-unused-function)

if(USE_GRAPHVIZ)
	target_compile_definitions(${PROJECT_NAME} PRIVATE USE_GRAPHVIZ)
	target_include_directories(${PROJECT_NAME} PRIVATE ${Graphviz_INCLUDE_DIRS})

	# 指定graphviz的库文件以及位置，防止链接时找不到graphviz的库函数
	target_link_libraries(${PROJECT_NAME} PRIVATE ${Graphviz_LIBRARIES})
endif()

# 引入graphviz库的头文件，防止编译时找不到graphviz的头文件
target_include_directories(${PROJECT_NAME} PRIVATE

	# ${ANTLR4_INCLUDE_DIR}
	utils
	symboltable
	ir
	ir/Generator
	ir/Types
	ir/Values
	ir/Instructions
	frontend

	# frontend/antlr4
	# frontend/antlr4/autogenerated
	frontend/flexbison
	frontend/flexbison/autogenerated
	backend
	backend/arm32
	backend/arm64
)

# 通过flex产生词法分析源代码
add_custom_command(
	OUTPUT
	${FLEX_OUTPUT}
	COMMAND
	${FLEX_EXECUTABLE} ${FLEX_OPTION} -o ${FLEX_GEN_DIR}/MiniCFlex.cpp --header-file=${FLEX_GEN_DIR}/MiniCFlex.h ${FLEX_INPUT}
	DEPENDS
	${FLEX_INPUT}
	WORKING_DIRECTORY
	${FLEX_GEN_DIR}
)

# 通过bison生成语法分析源代码
add_custom_command(
	OUTPUT
	${BISON_OUTPUT}
	COMMAND
	${BISON_EXECUTABLE} -Wcounterexamples --debug ${BISON_EXTRA_OPTION} -o ${FLEX_GEN_DIR}/MiniCBison.cpp --defines=${FLEX_GEN_DIR}/MiniCBison.h ${BISON_INPUT}
	DEPENDS
	${BISON_INPUT}
	WORKING_DIRECTORY
	${BISON_GEN_DIR}
)

if(BISON_OUTPUT_GRAPH)
	# 转换dot文件成png文件
	add_custom_command(
		TARGET ${PROJECT_NAME}
		POST_BUILD
		COMMAND
		dot -Tpng -o ${FLEX_GEN_DIR}/MiniCBison.png ${FLEX_GEN_DIR}/MiniCBison.dot
		COMMENT
		"dot2png"
		VERBATIM
	)
endif()

# 源代码打包
set(CPACK_SOURCE_GENERATOR "ZIP;TGZ")
set(CPACK_SOURCE_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}-src")

# 忽略不需要源代码目录或文件
set(CPACK_SOURCE_IGNORE_FILES
	"/build/;" # 忽略 build 目录
	"/cmake-build-.*/;" # cmake-build-开头的目录
	"/.git/;" # 忽略 Git 仓库
	"~$;" # 忽略临时文件（如编辑器产生的临时文件）
	"/doc/;" # 忽略 doc 目录

	# "/frontend/antlr4/.antlr/;" # antlr自动产生的目录
	"/.history/;" # 本地历史文件
	"/tests/;" # 测试文件夹
)

# 二进制打包
set(CPACK_GENERATOR "ZIP;TGZ")

# 安装可执行程序，不要bin文件夹下
install(TARGETS ${PROJECT_NAME} RUNTIME DESTINATION .)

# 设置打包的名字
set(CPACK_PACKAGE_FILE_NAME "${PROJECT_NAME}-${PROJECT_VERSION}-bin")

# 设置包描述
set(CPACK_PACKAGE_DESCRIPTION "minic表达式版")

include(CPack)
